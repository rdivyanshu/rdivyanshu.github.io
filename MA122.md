# Solving a mathematic problem using Rosette, solver aided programming environment
This blogpost is illustration of solver aided programming environment in [rosette](https://docs.racket-lang.org/rosette-guide/index.html) by way of solving crux mathematic problem. Rosette is environment where programming is assisted with SMT solver. This let us do [verification](https://docs.racket-lang.org/rosette-guide/ch_essentials.html#%28part._sec~3averify%29), [synthesis](https://docs.racket-lang.org/rosette-guide/ch_essentials.html#%28part._sec~3asynthesize%29), and [angelic execution](https://docs.racket-lang.org/rosette-guide/ch_essentials.html#%28part._sec~3asolve%29).

First the problem we are planning to tackle.

[MA122](https://cms.math.ca/wp-content/uploads/2021/06/CRUXv47n5-b.pdf)

Four people Mr Baker, Ms Carpenter, Mr Driver, and Ms Plumber are employed for four jobs as a baker, carpenter, driver, and plumber. None of them has a name identifying their occupation. They make four statements: 
	
  1. Mr Baker says he is the plumber.
  2. Mr Driver says he is the baker. 
  3. Ms Carpenter says she is not the plumber. 
  4. Ms Plumber says she is not the carpenter. 
  
Exactly ? (coffee stain) of the four statements are true. Who is the driver? (One of the editors apologizes for spilling coffee on the page, but we are sure the question used to have a unique answer!)

Rosette is a language implemented in racket which supports racket constructs plus additional solver aided forms. We begin with giving language directive to racket then proceeds with imports.

```
#lang rosette
(require rosette/lib/angelic)
```

It would be helpful if we define a name for all professions mentioned in the puzzle.

```
(define professions (list 'baker 'carpenter 'driver 'plumber))
```

Next we introduce assignment of profession to person with help of rosette construct `choose*`. We will using variable names for persons and symbols for professions. Here we are saying each person is assigned one of professions without assigning any concrete value. Actual assignment will be done by SMT solver considering all the constraints at hand.

```
(define baker (apply choose* profession))
(define carpenter (apply choose* profession))
(define driver (apply choose* profession))
(define plumber (apply choose* profession))
```

Let us list down constraints (fixed) which always hold. Each person has different profession and person name don't collide with profession name.

```
(define (f-constraints)
  (list (distinct? baker driver carpenter plumber)
        (not (equal? baker 'baker))
        (not (equal? driver 'driver))
        (not (equal? carpenter 'carpenter))
        (not (equal? plumber 'plumber))))
```

Further note down constraints (variable) which may or may not hold as some statements might be false.

```
(define (v-constraints)
  (list (equal? baker 'plumber)
        (equal? driver 'baker)
        (! (equal? carpenter 'plumber))
        (! (equal? plumber 'carpenter))))
```

We need helper method to find who is assigned given profession in solution. Let's write that down too.

```
(define (assigned profession sol)
  (first (filter (lambda (p) (equal? (evaluate p sol) profession))
          (list baker carpenter driver plumber))))
```

We are now at crux of our small program where we write function to fix assignment of person (variable) to profession (symbol) with help of SMT solver using rosette solver aided form [solve+](https://docs.racket-lang.org/rosette-guide/ch_syntactic-forms_rosette.html#%28def._%28%28lib._rosette%2Fquery%2Fform..rkt%29._solve%2B%29%29). First we find assignment with constraint given as parameter. If no assignment exists due constraints being too restricting we return empty list else we try to find another solution where `driver` profession is not assigned to same person as first solution. If such assignment exists we return these two solutions in list otherwise we return first solution in list.

```
(define (m-solve constraint)
  (define s-solve (solve+))
  (let ([sol (s-solve constraint)])
    (if (sat? sol)
        (letrec ([a-driver (assigned 'driver sol)]
                 [n-sol (s-solve (not (equal? 'driver a-driver)))])
            (cons sol (if (sat? n-sol) (list n-sol) (list))))
        (list))))
```

Finally let us solve the puzzle. We iterate through 0 to 4 assuming it is true variable constraints count which is anded with fixed constraints then asking for solution using `m-solve`. If only one solution exists we have found what we are looking for. Once we find that solution we ask profession assigned to each person. As you can see answer to our little puzzle is `baker`.

```
(define a-solutions 
    (filter (lambda (s) (= (length s) 1))
            (for/list ([t (range 5)])
                (m-solve (and (apply && (f-constraints))
                              (= t (length (filter identity (v-constraints)))))))))

(length a-solutions)
(define sol (first (first a-solutions)))
(evaluate (list baker carpenter driver plumber) sol)
```

```
Welcome to DrRacket, version 8.0 [cs].
Language: rosette, with debugging; memory limit: 128 MB.
1
'(driver plumber carpenter baker)
> 
```

Complete source code can be found [here](https://gist.github.com/rdivyanshu/41635e6d9075eac29e677dbfb025f18f).